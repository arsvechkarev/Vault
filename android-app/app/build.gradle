plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-parcelize'
}

java {
  toolchain {
    implementation = JvmImplementation.J9
  }
}

android {
  packagingOptions {
    resources {
      excludes += ['META-INF/LICENSE', 'META-INF/INDEX.LIST']
    }
  }
  File localProperties = file("../../local.properties")
  if (localProperties.exists()) {
    signingConfigs {
      Properties properties = new Properties()
      properties.load(new FileInputStream(localProperties))
      release {
        storeFile file(properties["keystoreFile"])
        storePassword properties["keystorePassword"]
        keyAlias properties["keyAlias"]
        keyPassword properties["keyPassword"]
      }
    }
  }
  def ext = rootProject.ext
  compileSdkVersion ext.compileSdkVersion
  defaultConfig {
    applicationId ext.applicationId
    minSdkVersion ext.minSdkVersion
    targetSdkVersion ext.targetSdkVersion
    versionCode ext.versionCode
    versionName ext.versionName
    if (localProperties.exists()) {
      signingConfig signingConfigs.release
    } else {
      signingConfig signingConfigs.debug
    }
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    debug {
      buildConfigField("String", "STUB_PASSWORD", "\"qwetu1233\"")
      applicationIdSuffix ".debug"
    }
    release {
      buildConfigField("String", "STUB_PASSWORD", "\"\"")
      minifyEnabled ext.minifyEnabled
      shrinkResources ext.shrinkResources
      proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
    }
  }
  kotlinOptions {
    jvmTarget = "11"
  }
  namespace 'com.arsvechkarev.vault'
}

dependencies {

  implementation project(':android-app:domain')
  implementation project(':android-app:navigation')
  implementation project(':android-app:viewdsl')

  def appDependencies = rootProject.ext.applicationDependencies
  implementation appDependencies.kotlin
  implementation appDependencies.appCompat
  implementation appDependencies.coroutinesCore
  implementation appDependencies.coroutinesAndroid
  implementation appDependencies.coordinatorLayout
  implementation appDependencies.constraintLayout
  implementation appDependencies.recyclerView
  implementation appDependencies.kotpass
  implementation appDependencies.okio
  implementation appDependencies.zxcvbn
  implementation appDependencies.timber
  implementation appDependencies.lifecycleRuntime
  implementation appDependencies.coil
  implementation appDependencies.biometrics
  implementation appDependencies.documentfile

  def testDependencies = rootProject.ext.testDependencies

  testImplementation testDependencies.junit

  androidTestImplementation testDependencies.kaspresso
  androidTestImplementation testDependencies.espressoIntents
  androidTestImplementation testDependencies.androidTestRunner
  androidTestImplementation testDependencies.androidTestRules
  androidTestImplementation testDependencies.androidTestExt
  androidTestImplementation testDependencies.androidTestCoreKtx
}
